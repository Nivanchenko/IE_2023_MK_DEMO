#Использовать entity
#Использовать fs

Перем МенеджерСущностей;

&Желудь
Процедура ПриСозданииОбъекта(
	
	&Деталька(Значение = "НастройкиПодключенияКБазеДанных.СтрокаСоединения")
	СтрокаСоединения,

	&Деталька(Значение = "НастройкиПодключенияКБазеДанных.ТипКоннектора")
	ТипКоннектора,

	&Пластилин(Значение = "Сущность", Тип = "ТаблицаЗначений") 
	ТаблицаСущностей
)

	МенеджерСущностей = Новый МенеджерСущностей(Тип(ТипКоннектора), 
												СтрокаСоединения);
	
	Для каждого ТекСущность Из ТаблицаСущностей Цикл
		МенеджерСущностей.ДобавитьКлассВМодель(ТекСущность.ОпределениеЖелудя.ТипЖелудя());	
	КонецЦикла;
	
	МенеджерСущностей.Инициализировать();

КонецПроцедуры

Функция ЗаписатьНовыйОтчет(Пользователь, Ошибка, Комментарий) Экспорт

	Отчет = Новый ОписаниеОтчетаОшибки();
	Отчет.Пользователь = Пользователь;
	Отчет.Ошибка = Ошибка;
	Отчет.Комментарий = Комментарий;
	Отчет.Дата = ТекущаяДата();

	Попытка
		МенеджерСущностей.Сохранить(Отчет);	
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
	

	Возврат Строка(Отчет.Идентификатор);
	
КонецФункции

Функция ПолучитьОтчеты() Экспорт
	Возврат МенеджерСущностей.Получить(Тип("ОписаниеОтчетаОшибки"));
КонецФункции

Функция ПолучитьОтчетПоИдентификатору(Идентификатор) Экспорт
	Сообщить("Идентификатор " + Идентификатор);
	Возврат МенеджерСущностей.ПолучитьОдно(Тип("ОписаниеОтчетаОшибки"), Число(Идентификатор));
КонецФункции