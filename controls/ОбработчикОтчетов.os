#Использовать tempfiles

&Деталька
Перем КаталогСохраненияКартинок;

&Пластилин
Перем ХранилищеОтчетов;

&Пластилин Перем РегистраторКаталогаФайлов;

&Желудь
Процедура ПриСозданииОбъекта()
	
КонецПроцедуры

Функция РазобратьОтчет(ДвоичныеДанныеОтчета)
	ВременныйКаталог = ВременныеФайлы.СоздатьКаталог();
	Архиватор = Новый ЧтениеZipФайла(ДвоичныеДанныеОтчета.ОткрытьПотокДляЧтения());
	Архиватор.ИзвлечьВсе(ВременныйКаталог);

	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.ОткрытьФайл(ОбъединитьПути(ВременныйКаталог, "report.json"));
	СтруктураОтчета = ПрочитатьJSON(ЧтениеJSON);
	ЧтениеJSON.Закрыть();

	Отчет = Новый Структура("Данные, Картинка", СтруктураОтчета, 
												Новый ДвоичныеДанные(ОбъединитьПути(ВременныйКаталог, "screenshot.png")));
	ВременныеФайлы.Удалить();

	Возврат Отчет;
КонецФункции

Процедура СохранитьОтчет(ДвоичныеДанныеОтчета) Экспорт

	Отчет = РазобратьОтчет(ДвоичныеДанныеОтчета);

	Комментарий = "";

	Отчет.Данные.errorInfo.Свойство("userDescription", Комментарий);

	Идентификатор = ХранилищеОтчетов.ЗаписатьНовыйОтчет(Отчет.Данные.sessionInfo.userName,
														Отчет.Данные.errorInfo.applicationErrorInfo.errors[0][0],
														Комментарий);

	Попытка
		ПутьДоКартинки = ОбъединитьПути(КаталогСохраненияКартинок, Идентификатор + ".png");
		Отчет.Картинка.Записать(ПутьДоКартинки);
		РегистраторКаталогаФайлов.ОбновтитьСтатичныеФайлы();
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры